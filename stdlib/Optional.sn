import[[use_macros]] Core::CLib;
import Core::Ptr;

/**
 * @brief The error thrown when an `Option` is empty.
 */
public class BadOptionAccess extends Exception
  {}
/**
 * A type that represents either a value or nothing at all.
 *
 * The Option type is used in many places becaimportit encodes the very common
 * scenario in which a value could be something or it could be nothing.
 *
 * For example, the `find` method on containers like `Array` and `Hash` returns
 * an `Option` becaimportthe element may or may not exist in the container.
 *
 * The `Option` type is also used in the return type of functions that are not
 * guaranteed to return a value, like `Array::pop` or `Hash::delete`.
 */
public class Option<T: Sized> {
    /**
     * The value stored in the `Option`.
     */
    let _value: T = @zero_initialized(:T);
    /**
     * Whether the `Option` is engaged.
     */
    let _engage: bool = false;
  public:
    /**
     * Creates a new empty `Option`.
     */
    Option() {}
    /**
     * Creates a new `Option` with the given value.
     *
     * @param value The value to store in the `Option`.
     */
    Option(value: T) : _value(value), 
      _engage(true) {}
    /**
     * @brief Returns the value stored in the `Option`.
     */
    func [[inline]] val() T
    {
      self.assert_not_empty();
      return self._value;
    }
    /**
     * @brief Returns whether the `Option` is empty.
     */
    func [[inline]] empty() bool 
    {
      return !self._engage;
    }
    /**
     * @brief Whether the `Option` is engaged.
     */
    func [[inline]] has_value() bool
      { return self._engage; }
    /**
     * @return The value stored in the `Option` or the
     *        given default value if the `Option` is empty.
     */
    func [[inline]] value_or(default_value: T) T
    {
      if self.empty() {
        return default_value;
      }
      return self.val();
    }
  private:
    /**
     * @brief Throws an error if the `Option` is empty.
     */
    func [[inline]] assert_not_empty()
    {
      if (self.empty()) {
        throw new BadOptionAccess("Attempted to access empty Option");
      }
    }
}
/**
 * @brief Creates a new `Option` with the given value.
 * @param value The value to store in the `Option`.
 */
public func [[inline]] Some<_StoreType>(value: _StoreType) Option<_StoreType>
  { return new Option<_StoreType>(value); }
/**
 * @brief Creates a new empty `Option`.
 */
public func [[inline]] None<_StoreType>() Option<_StoreType>
  { return new Option<_StoreType>(); }
  